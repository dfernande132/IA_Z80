  10 REM ============================================
  20 REM NAIVE BAYES (SINCLAIR BASIC) - 3 CLASES / 3 FEATURES
  30 REM Clases: 1=MAGO  2=GUERRERO  3=EXPLORADOR
  40 REM Features (0..100): MAGIA, FUERZA, VELOCIDAD
  50 REM Discretizacion: 0..33=BAJA, 34..66=MEDIA, 67..100=ALTA
  60 REM Suavizado Laplace: (conteo+1)/(totalClase+L), L=3
  70 REM ============================================

  80 LET N=30
  90 LET NC=3  : REM numero de clases
 100 LET NF=3: REM numero de features (magia,fuerza,velocidad)
 110 LET L=3: REM niveles (baja,media,alta)

 120 DIM M(N): DIM F(N): DIM V(N): DIM C(N)
 130 DIM K(NC)            : REM conteo por clase 
 140 DIM T(NC,NF,L)       : REM conteos condicionados 
 150 DIM S(NC)            : REM  SCORE

 160 REM ---- Cargar dataset ----
 170 RESTORE 9000
 180 FOR I=1 TO N
 190   READ M(I),F(I),V(I),C(I)
 200 NEXT I

 210 REM ---- Entrenamiento: inicializar ----
 220 FOR Z=1 TO NC
 230   LET K(Z)=0
 240 NEXT Z

 250 FOR Z=1 TO NC
 260   FOR J=1 TO NF
 270     FOR LV=1 TO L
 280       LET T(Z,J,LV)=0
 290     NEXT LV
 300   NEXT J
 310 NEXT Z

 320 FOR I=1 TO N
 330   LET Z=C(I)
 340   LET K(Z)=K(Z)+1
 350   LET X=M(I): GO SUB 900: LET LV1=X
 360   LET X=F(I): GO SUB 900: LET LV2=X
 370   LET X=V(I): GO SUB 900: LET LV3=X

 380   LET T(Z,1,LV1)=T(Z,1,LV1)+1
 390   LET T(Z,2,LV2)=T(Z,2,LV2)+1
 400   LET T(Z,3,LV3)=T(Z,3,LV3)+1
 410 NEXT I

 420 CLS
 430 PRINT "NAIVE BAYES: ":PRINT " MAGO / GUERRERO / EXPLORADOR"
 440 PRINT "Dataset: 30 ejemplos (igual que k-NN)"
 450 PRINT

 460 INPUT "MAGIA (0-100)? ";QM
 470 INPUT "FUERZA (0-100)? ";QF
 480 INPUT "VELOCIDAD (0-100)? ";QV

 490 IF QM<0 THEN LET QM=0
 500 IF QM>100 THEN LET QM=100
 510 IF QF<0 THEN LET QF=0
 520 IF QF>100 THEN LET QF=100
 530 IF QV<0 THEN LET QV=0
 540 IF QV>100 THEN LET QV=100

 550 LET X=QM: GO SUB 900: LET QLVA=X
 560 LET X=QF: GO SUB 900: LET QLVB=X
 570 LET X=QV: GO SUB 900: LET QLVC=X

 580 REM ---- Calcular scores por clase ----
 590 FOR Z=1 TO NC
 610   LET P = K(Z)/N
 630   LET PA = (T(Z,1,QLVA)+1)/(K(Z)+L)
 640   LET PB = (T(Z,2,QLVB)+1)/(K(Z)+L)
 650   LET PC = (T(Z,3,QLVC)+1)/(K(Z)+L)
 660   LET S(Z)=P*PA*PB*PC
 670 NEXT Z

 680 REM ---- Elegir clase con mayor score ----
 690 LET WIN=1: LET BEST=S(1)
 700 IF S(2)>BEST THEN LET BEST=S(2): LET WIN=2
 710 IF S(3)>BEST THEN LET BEST=S(3): LET WIN=3

 720 PRINT
 730 PRINT "NIVELES (1=Baja,2=Media,3=Alta):"
 740 PRINT "MAGIA=";QLVA;" FUERZA=";QLVB;" VELOCIDAD=";QLVC
 750 PRINT

 760 PRINT "SCORES (proporcionales):"
 770 PRINT "MAGO      : ";S(1)
 780 PRINT "GUERRERO  : ";S(2)
 790 PRINT "EXPLORADOR: ";S(3)
 800 PRINT: INK 1

 810 IF WIN=1 THEN PRINT "CLASE ESTIMADA: MAGO"
 820 IF WIN=2 THEN PRINT "CLASE ESTIMADA: GUERRERO"
 830 IF WIN=3 THEN PRINT "CLASE ESTIMADA: EXPLORADOR"

 840 PRINT: INK 0
 850 PRINT "Cambia los rangos para ver su   efecto."
 860 PAUSE(0):STOP

900 REM ============================================
910 REM DISCRETIZA X (0..100) EN 3 NIVELES Y LO DEVUELVE EN X
920 REM 1=BAJA (0..33), 2=MEDIA (34..66), 3=ALTA (67..100)
930 REM ============================================

950 IF X<=33 THEN LET X=1: RETURN
960 IF X<=66 THEN LET X=2: RETURN
970 LET X=3
980 RETURN

8999 REM ============================================
9000 REM DATA: (MAGIA, FUERZA, VELOCIDAD, CLASE)
9010 REM 1=MAGO 2=GUERRERO 3=EXPLORADOR
9020 REM (mismo dataset que en k-NN)
9030 REM ============================================

9040 REM --- MAGOS ---
9050 DATA 90,20,40,1
9060 DATA 85,25,55,1
9070 DATA 95,15,35,1
9080 DATA 80,30,50,1
9090 DATA 88,18,60,1
9100 DATA 92,22,45,1
9110 DATA 78,28,65,1
9120 DATA 83,20,30,1
9130 DATA 87,35,40,1
9140 DATA 93,10,55,1

9150 REM --- GUERREROS ---
9160 DATA 15,90,40,2
9170 DATA 20,85,35,2
9180 DATA 10,95,30,2
9190 DATA 25,80,50,2
9200 DATA 18,88,55,2
9210 DATA 12,92,45,2
9220 DATA 30,78,40,2
9230 DATA 22,83,60,2
9240 DATA 17,97,25,2
9250 DATA 28,86,35,2

9260 REM --- EXPLORADORES ---
9270 DATA 25,45,90,3
9280 DATA 20,50,85,3
9290 DATA 30,40,95,3
9300 DATA 35,55,80,3
9310 DATA 18,60,88,3
9320 DATA 40,42,92,3
9330 DATA 22,48,78,3
9340 DATA 15,52,96,3
9350 DATA 32,38,89,3
9360 DATA 27,58,83,3