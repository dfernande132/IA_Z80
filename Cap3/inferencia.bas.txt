  10 REM ============================================
  20 REM  MINI SISTEMA EXPERTO (SINCLAIR BASIC)
  30 REM  DIAGNOSTICO: PROBLEMAS AL CARGAR DESDE CINTA
  40 REM  Hechos: -1 desconocido, 0 falso, 1 verdadero
  50 REM ============================================

  55 LET NOBS=7:REM Hechos observables: 1..7
  56 LET MAXF=50:REM hechos soportados
  57 LET MAXR=50:REM reglas soportadas

  60 DIM F(MAXF)
  70 DIM C(MAXR): DIM D(MAXR): DIM E(MAXR)

  80 REM --- CARGAR REGLAS (Base de Conocimiento) ---
  90 LET NR=0
 100 LET NF=NOBS
 110 RESTORE 9000

 120 REM Leemos reglas hasta DATA 0,0,0
 130 READ A,B,T
 140 IF A=0 AND B=0 AND T=0 THEN GO TO 200

 150 LET NR=NR+1
 160 LET C(NR)=A: LET D(NR)=B: LET E(NR)=T

 170 LET X=ABS A: IF X>NF THEN LET NF=X
 175 LET X=ABS B: IF X>NF THEN LET NF=X
 180 IF T>NF THEN LET NF=T

 190 GO TO 130

 200 REM --- INICIALIZAR HECHOS ---
 210 FOR I=1 TO NF: LET F(I)=-1: NEXT I

 220 CLS
 230 PRINT "SISTEMA EXPERTO:"
 240 PRINT "CARGA DESDE CINTA"
 250 PRINT "Responde S o N Mayusculas"
 260 PRINT

 270 REM --- HECHOS OBSERVABLES (Memoria de Trabajo) ---
 280 GO SUB 2000

 290 REM --- MOTOR DE INFERENCIA ---
 300 GO SUB 3000

 310 REM --- INFORME FINAL ---
 320 GO SUB 4000
 330 PAUSE 0: STOP

 999 REM ============================================
1000 REM 2000) PREGUNTAS AL USUARIO (hechos base)
1010 REM ============================================
2000 PRINT "1)USAS CARGA DESDE CINTA? (S/N)";
2010 GO SUB 7000: LET F(1)=ANS
2020 IF F(1)=0 THEN PRINT:PRINT "OK. Este experto es solo para cinta.": STOP
2030 PRINT
2040 PRINT "2)EL VOLUMEN DEL REPRODUCTOR ESTA ALTO?";
2050 GO SUB 7000: LET F(2)=ANS
2055 PRINT
2060 PRINT "3)LA SALIDA ES AURICULARES/LINE OUT?";
2070 GO SUB 7000: LET F(3)=ANS
2075 PRINT
2080 PRINT "4)EL CABLE ES CORRECTO Y ESTA CONECTADO?";
2090 GO SUB 7000: LET F(4)=ANS
2095 PRINT
2100 PRINT "5)VES RAYAS DE CARGA EN BORDE?";
2110 GO SUB 7000: LET F(5)=ANS
2115 PRINT
2120 PRINT "6)SALE 'R Tape loading error'?";
2130 GO SUB 7000: LET F(6)=ANS
2135 PRINT
2140 PRINT "7)OTRA CINTA TAMBIEN FALLA?";
2150 GO SUB 7000: LET F(7)=ANS
2155 PRINT
2160 PRINT
2170 RETURN

2999 REM ============================================
3000 REM 3000) MOTOR DE INFERENCIA (encadenamiento)
3001 REM Reglas: SI C1 y C2 ENTONCES TH
3002 REM Si C2=0 significa "solo C1"
3003 REM Soporta negacion: si C1=-4 => F(4) debe ser 0
3004 REM ============================================
3020 LET CAMBIO=0
3030 FOR R=1 TO NR
3040   LET OK=1

3050   LET A=C(R)
3060   IF A<>0 THEN IF A>0 THEN IF F(A)<>1 THEN LET OK=0
3065   IF A<>0 THEN IF A<0 THEN IF F(-A)<>0 THEN LET OK=0

3070   LET B=D(R)
3080   IF B<>0 THEN IF B>0 THEN IF F(B)<>1 THEN LET OK=0
3085   IF B<>0 THEN IF B<0 THEN IF F(-B)<>0 THEN LET OK=0

3090   IF OK=1 THEN LET T=E(R): IF T>0 THEN IF F(T)<>1 THEN LET F(T)=1: LET CAMBIO=1
3100 NEXT R

3110 IF CAMBIO=1 THEN GO TO 3020
3120 RETURN

3999 REM ============================================
4000 REM 4000) CONCLUSIONES Y RECOMENDACIONES
4010 REM ============================================
4015 PRINT "--------------------------------"
4020 PRINT "RESULTADO:" : PRINT

4035 INK 1
4040 IF F(8)=1 THEN PRINT "- Ajusta VOL y prueba de nuevo."
4050 IF F(9)=1 THEN PRINT "- Usa LINE OUT, no MIC."
4060 IF F(10)=1 THEN PRINT "- Revisa cable / conector EAR"
4070 IF F(11)=1 THEN PRINT "- La cinta puede estar desmagnetizada."
4080 IF F(12)=1 THEN PRINT "- Posible problema de cabezal (azimut)."
4085 INK 0

4090 LET ANY=0
4100 FOR I=8 TO 12
4110 IF F(I)=1 THEN LET ANY=1
4120 NEXT I

4135 INK 3
4140 IF ANY=0 THEN PRINT "- No tengo suficiente evidencia. Repite y revisa conexiones."
4145 INK 0

4160 PRINT "--------------------------------"
4170 RETURN

6999 REM ============================================
7000 REM 7000) LECTOR S/N (devuelve ANS=1 si S, 0 si N)
7001 REM ============================================
7005 INPUT A$
7015 IF A$="" THEN GO TO 7000
7030 IF A$="S" OR A$="s" THEN LET ANS=1: RETURN
7040 IF A$="N" OR A$="n" THEN LET ANS=0: RETURN
7050 PRINT "Responde S o N."
7060 GO TO 7000


9000 REM ============================================
9010 REM BASE DE CONOCIMIENTO (REGLAS)
9020 REM Formato: C,D,E
9030 REM ============================================

9070 REM --- Error: recomendaciones basicas condicionadas ---
9080 DATA 6,-2,8:REM error + volumen NO alto -> subir volumen
9090 DATA 6,-3,9:REM error + NO lineout -> usar lineout
9100 DATA 6,-4,10:REM error + cable NO OK -> revisar cable

9110 REM --- Error: diagnOstico ---
9120 DATA 6,5,12:REM error + rayas -> cabezal/azimut
9130 DATA 6,4,12:REM error + cable OK -> posible cabezal/azimut

9140 REM --- Otra cinta falla: diagnostico ---
9150 DATA 7,5,11:REM otra cinta falla + rayas -> cinta mala/desmagnetizada
9160 DATA 7,-5,10:REM otra cinta falla + NO rayas -> revisar cable/entrada

9170 REM --- Refuerzos ---
9180 DATA 5,-4,10:REM rayas + cable NO OK -> revisar cable
9190 DATA 5,4,12:REM rayas + cable OK -> azimut probable
9200 DATA 6,7,12:REM error + otra cinta falla -> cabezal/azimut probable

9210 DATA 0,0,0:REM FIN DE REGLAS