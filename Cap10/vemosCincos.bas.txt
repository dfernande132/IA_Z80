10 REM *** PERCEPTRON 6X6: RECONOCE EL "5" ***
20 LET N = 8: REM 4 POSITIVOS + 4 NEGATIVOS
25 LET M = 36: REM 6x6 = 36 PIXELES
30 DIM X(N,M): DIM T(N): DIM W(M)
40 REM --- DATOS DE ENTRENAMIENTO (36 BITS + ETIQUETA) ---
41 REM FORMATO: 6 FILAS DE 6 (TOTAL 36), Y AL FINAL T (1=ES UN 5, 0=NO)
45 REM --- 4 EJEMPLOS POSITIVOS: "5" (CON VARIACIONES) ---
50 DATA 1,1,1,1,1,1,  1,0,0,0,0,0,  1,1,1,1,1,0,  0,0,0,0,0,1,  0,0,0,0,0,1,  1,1,1,1,1,0,  1
60 DATA 1,1,1,1,1,0,  1,0,0,0,0,0,  1,1,1,1,0,0,  0,0,0,0,1,0,  0,0,0,0,1,0,  1,1,1,1,1,1,  1
70 DATA 0,1,1,1,1,0,  1,0,0,0,0,0,  1,1,1,1,1,0,  0,0,0,0,0,1,  0,0,0,0,1,0,  0,1,1,1,1,0,  1
80 DATA 0,1,1,1,1,0,  1,0,0,0,0,0,  0,1,1,1,1,0,  0,0,0,0,0,1,  0,1,1,1,1,0,  1,0,0,0,0,0,  1
85 REM --- 4 EJEMPLOS NEGATIVOS: NO ES "5" ---
90 REM "0" (MARCO)
91 DATA  0,1,1,1,1,0,  1,0,0,0,0,1,  1,0,0,0,0,1,  1,0,0,0,0,1,  1,0,0,0,0,1,  0,1,1,1,1,0,  0
100 REM "1" (COLUMNA DERECHA)
101 DATA 0,0,0,0,1,1,  0,0,0,0,1,0,  0,0,0,0,1,0,  0,0,0,0,1,0,  0,0,0,0,1,0,  0,0,0,1,1,1,  0
110 REM "2" (PARECIDO PERO DISTINTO)
111 DATA 0,1,1,1,1,0,  0,0,0,0,0,1,  0,0,0,0,1,0,  0,0,0,1,0,0,  0,0,1,0,0,0,  1,1,1,1,1,1,  0
120 REM "8" (MUY PARECIDO)
121 DATA 1,1,1,1,1,1,  1,0,0,0,0,1,  1,1,1,1,1,1,  1,0,0,0,0,1,  1,0,0,0,0,1,  1,1,1,1,1,1,  0
130 REM --- CARGAR DATOS ---
140 FOR F=1 TO N
150 FOR P=1 TO M
160 READ X(F,P)
170 NEXT P
180 READ T(F)
190 NEXT F

200 REM --- INICIALIZAR ---
210 FOR P=1 TO M: LET W(P)=RND-0.5: NEXT P
220 LET B=RND-0.5
230 LET LR = 0.1: REM TASA DE APRENDIZAJE

240 PRINT "ENTRENANDO DETECTOR DEL 5 EN 6x6"
250 FOR E=1 TO 200
255 PRINT AT 2,0;"EPOCA ACTUAL ";E
260 LET ET = 0
270 FOR I = 1 TO N
280 LET Z = B
290 FOR P = 1 TO M
300 LET Z = Z + X(I,P)*W(P)
310 NEXT P
320 IF Z > 0 THEN LET Y=1
330 IF Z <= 0 THEN LET Y=0
340 LET ERR = T(I) - Y
350 IF ERR <> 0 THEN LET ET = ET + 1
360 FOR P = 1 TO M
370 LET W(P) = W(P) + LR*ERR*X(I,P)
380 NEXT P
390 LET B = B + LR*ERR
400 NEXT I
410 IF ET = 0 THEN GOTO 440
420 NEXT E
430 INK 5:PRINT AT 1,0;"AVISO: NO CONVERGE TOTALMENTE":INK 0
440 PRINT AT 2,0; "Â¡ENTRENADO EN ";E;" EPOCAS!":INK 1:PRINT  "ERRORES LA ULTIMA = ";ET: INK 0

450 REM --- TEST INTERACTIVO ---
460 PRINT "DIBUJA UN 6x6 (36 BITS). ":PRINT "EJEMPLO 111111100000111110000001000001111110"
470 PRINT "ESCRIBE FIN PARA SALIR"
480 INPUT A$
490 IF A$="FIN" THEN STOP
500 IF LEN A$ <> 36 THEN PRINT "ERROR: NECESITO 36 PIXELES": GOTO 480

510 REM --- CALCULAR Z CON EL PATRON INTRODUCIDO ---
520 LET Z=B
530 FOR P=1 TO 36
540 LET Z=Z + (CODE(A$(P)) - 48)*W(P)
550 NEXT P

560 INK 3
565 PRINT A$
570 IF Z > 0 THEN PRINT "    ES UN 5"
580 IF Z <= 0 THEN PRINT "   NO ES UN 5"
590 INK 0
600 GOTO 480
