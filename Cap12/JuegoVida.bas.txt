10 REM ==========================================
15 REM  JUEGO DE LA VIDA 20x20 (TOROIDAL)
20 REM  EDITOR + SIMULACION / DOBLE BUFFER REAL
25 REM  QAOP MOVER, SPACE TOGGLE, ENTER RUN
30 REM  C LIMPIA, R RANDOM, E EDIT, X SALIR
35 REM ==========================================

45 LET N=20

46 REM --- CADENA PARA MARCO (20 GUIONES) ---
47 LET F$=""
48 FOR I=1 TO N
49 LET F$=F$+"-"
50 NEXT I

55 REM --- DOBLE BUFFER (DOS TABLEROS 2D) ---
60 DIM M(N,N)
61 DIM S(N,N)
65 LET T=1
70 LET G=0

75 REM --- TABLAS TOROIDALES (ARRIBA/ABAJO/IZQ/DER) ---
80 DIM U(N): DIM W(N): DIM P(N): DIM Q(N)
85 FOR I=1 TO N
90 LET U(I)=I-1: IF U(I)=0 THEN LET U(I)=N
95 LET W(I)=I+1: IF W(I)=N+1 THEN LET W(I)=1
100 LET P(I)=I-1: IF P(I)=0 THEN LET P(I)=N
105 LET Q(I)=I+1: IF Q(I)=N+1 THEN LET Q(I)=1
110 NEXT I

115 REM --- LIMPIAR TABLERO ACTUAL ---
120 FOR R=1 TO N: FOR C=1 TO N: LET M(R,C)=0: LET S(R,C)=0: NEXT C: NEXT R

125 REM --- CURSOR INICIAL ---
130 LET CR=10: LET CX=10

135 REM --- ENTRAR AL EDITOR ---
140 GO SUB 1000
145 GO TO 200

200 REM ====== BUCLE DEL EDITOR (ACTUALIZA SOLO 2-3 CELDAS) ======
205 LET K$=INKEY$
210 PAUSE 15
215 IF K$="" THEN GO TO 205

220 LET PR=CR: LET PX=CX

225 IF K$="q" OR K$="Q" THEN LET CR=CR-1
230 IF K$="a" OR K$="A" THEN LET CR=CR+1
235 IF K$="o" OR K$="O" THEN LET CX=CX-1
240 IF K$="p" OR K$="P" THEN LET CX=CX+1

245 IF CR<1 THEN LET CR=1
250 IF CR>N THEN LET CR=N
255 IF CX<1 THEN LET CX=1
260 IF CX>N THEN LET CX=N

265 REM --- TOGGLE CON SPACE ---
270 IF K$=" " THEN LET M(CR,CX)=1-M(CR,CX): LET RR=CR: LET CC=CX: LET INV=1: GO SUB 1200

275 REM --- LIMPIAR ---
280 IF K$="c" OR K$="C" THEN FOR R=1 TO N: FOR C=1 TO N: LET M(R,C)=0: NEXT C: NEXT R: GO SUB 1000: GO TO 200

285 REM --- RANDOM ---
290 IF K$="r" OR K$="R" THEN GO SUB 1500: GO SUB 1000: GO TO 200

295 REM --- SALIR ---
300 IF K$="x" OR K$="X" THEN STOP

305 REM --- ENTER: INICIAR SIMULACION ---
310 IF CODE K$=13 THEN GO TO 2000

315 REM --- SI SOLO SE MOVIO EL CURSOR, REDIBUJA 2 CELDAS ---
320 IF PR<>CR OR PX<>CX THEN LET RR=PR: LET CC=PX: LET INV=0: GO SUB 1200: LET RR=CR: LET CC=CX: LET INV=1: GO SUB 1200
325 GO TO 200

1000 REM ==========================================
1010 REM  DIBUJAR TABLERO EN MODO EDICION
1020 REM ==========================================
1030 CLS
1040 INK 3
1042 PRINT AT 4,20;"==========="
1044 PRINT AT 5,20;"EL JUEGO DE"
1046 PRINT AT 6,20;"   LA VIDA  "
1048 PRINT AT 7,20;"==========="
1050 INK 0
1052 PRINT AT 0,0;"QAOP+SPACE, ENTER ejecutar"
1054 PRINT "C Limpia, R Random, X Salir"
1056 PRINT

1060 LET BR=2: LET BC=0

1065 FOR R=1 TO N
1070 FOR C=1 TO N
1075 LET L$="."
1080 IF M(R,C)=1 THEN LET L$="*"
1085 IF R=CR AND C=CX THEN INVERSE 1
1090 PRINT AT BR+R-1, BC+C-1; L$;
1095 INVERSE 0
1100 NEXT C
1105 NEXT R

1110 RETURN

1200 REM ==========================================
1210 REM  DIBUJA UNA CELDA (EDITOR)
1220 REM ==========================================
1230 LET L$="."
1235 IF M(RR,CC)=1 THEN LET L$="*"
1240 IF INV=1 THEN INVERSE 1
1245 PRINT AT BR+RR-1, BC+CC-1; L$;
1250 INVERSE 0
1255 RETURN

1500 REM ==========================================
1510 REM  RELLENO ALEATORIO (DENSIDAD ~ 25%)
1520 REM ==========================================
1530 FOR R=1 TO N
1535 FOR C=1 TO N
1540 LET M(R,C)=0
1545 IF RND<0.25 THEN LET M(R,C)=1
1550 NEXT C
1555 NEXT R
1560 RETURN

2000 REM ==========================================
2010 REM  SIMULACION: GENERACIONES DEL JUEGO (RAPIDA)
2020 REM ==========================================
2030 LET G=0
2040 CLS
2050 INK 3: PRINT "                       EL JUEGO "
2053 PRINT "                      DE LA VIDA"
2056 INK 5:PRINT "                       E=EDITAR"
2058 PRINT "                       X=SALIR":INK 0
2060 GO SUB 3000

2065 REM ---- BUCLE PRINCIPAL SIMULACION ----
2070 LET G=G+1
2080 INK 1: PRINT AT 8,23;"GEN=";G;" ":INK 0

2100 FOR R=1 TO N
2110 LET RU=U(R): LET RD=W(R)
2120 FOR C=1 TO N
2130 LET LC=P(C): LET RC=Q(C)
2140 LET V = M(RU,LC)+M(RU,C)+M(RU,RC)
2150 LET V = V + M(R,LC)      +M(R,RC)
2160 LET V = V + M(RD,LC)+M(RD,C)+M(RD,RC)

2170 REM --- REGLA ---
2180 LET S(R,C)=((V=3) OR (M(R,C)=1 AND V=2)) AND 1
2190 NEXT C
2200 NEXT R

2210 REM --- SWAP: COPIAR S -> M ---
2220 FOR R=1 TO N: FOR C=1 TO N: LET M(R,C)=S(R,C): NEXT C: NEXT R

2230 GO SUB 3000
2270 GO TO 2070

3000 REM ==========================================
3010 REM  DIBUJO CON MARCO + BLOQUE/ESPACIO
3020 REM ==========================================
3030 LET BR=1: LET BC=0
3040 LET V$=CHR$ 143: LET M$=" "

3050 PRINT AT BR-1,BC;"+";F$;"+" 

3060 FOR R=1 TO N
3070 LET L$=""
3080 FOR C=1 TO N
3090 IF M(R,C)=1 THEN LET L$=L$+V$
3100 IF M(R,C)=0 THEN LET L$=L$+M$
3110 NEXT C
3120 PRINT AT BR+R-1,BC;"|";L$;"|"
3130 NEXT R

3140 PRINT AT BR+N,BC;"+";F$;"+" 
3145 LET K$=INKEY$
3150 IF K$="x" OR K$="X" THEN STOP
3155 IF K$="e" OR K$="E" THEN GO SUB 1000: GO TO 200
3160 PAUSE 1
3165 RETURN
