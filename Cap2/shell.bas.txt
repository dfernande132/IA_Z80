10 REM --- LABORATORIO DE RENDIMIENTO: SHELL SORT ---
15 RUN AT 3: REM Ponemos el Z80 a 28 MHz.
20  LET n = 300
30  DIM a(n)
40  REM LLAMAMOS A LA FUNCION QUE NOS GENERA NUMEROS ALEATORIOS
50  PROC genRand(n)
60  PRINT "Ordenando "; n; " elementos..."
70 LET tinicio= TIME
80 REM LLAMAMOS A LA FUNCION DE ORDENACION
90  PROC shellSort(n)
100 LET tfinal = TIME
120 LET transcurrido = (tfinal - tinicio) / 50
130 PRINT "Completado en ";transcurrido;" segundos."
140 REM --- COMPROBAMOS SI EL ARRAY ESTA ORDENADO ---
145 LET OK = 1
150 FOR i=1 TO n-1
160 IF a(i) > a(i+1) THEN LET OK =0
170 NEXT i
180 IF OK=1 THEN PRINT "Array correctamente ordenado.": ELSE PRINT "Error en el ordenamiento."
200 PAUSE 0: STOP

2000 DEFPROC genRand(n)
2010   LOCAL i
2020   FOR i = 1 TO n: LET a(i) = INT(RND * 1000): NEXT i
2030 ENDPROC

4000 DEFPROC shellSort(n)
4010   LOCAL g, i, j, t
4020   LET g = INT(n / 2): REM Definir el salto inicial
4030   REPEAT : WHILE g > 0
4040     FOR i = g + 1 TO n
4050       LET t = a(i)
4060       LET j = i
4070       REPEAT
4080         WHILE j > g
4090         WHILE a(j - g) > t
4100         LET a(j) = a(j - g)
4110         LET j = j - g
4120       REPEAT UNTIL 0
4130       LET a(j) = t
4140     NEXT i
4150     LET g = INT(g / 2)
4160   REPEAT UNTIL 0
4170 ENDPROC