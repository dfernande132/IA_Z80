  10 REM *** PERCEPTRON MULTICLASE (3 CLASES) ***
  20 REM CLASES: 1=MAGO, 2=GUERRERO, 3=EXPLORADOR

  30 LET N=30
  40 LET M=3
  50 LET K=3

  60 LET ZX=N*M
  70 LET ZW=K*M

  80 DIM X(ZX)
  90 DIM C(N)
 100 DIM W(ZW)
 110 DIM B(K)

 120 REM --- CARGAR DATOS (NORMALIZADOS /100) ---
 130 RESTORE 9000
 140 FOR I=1 TO N
 150 FOR P=1 TO M
 160 READ V
 170 LET X((I-1)*M+P)=V/100
 180 NEXT P
 190 READ C(I)
 200 NEXT I

 210 REM --- INICIALIZAR PESOS Y SESGOS ---
 220 FOR J=1 TO K
 230 FOR P=1 TO M
 240 LET W((J-1)*M+P)=RND-0.5
 250 NEXT P
 260 LET B(J)=RND-0.5
 270 NEXT J

 280 LET R=0.05
 290 INK 3:PRINT "ENTRENANDO CONSEJO DE SABIOS"
 300 PRINT "   ...(MULTICLASE)...": INK 0:

 310 REM --- ENTRENAMIENTO ONE-VS-REST ---
 315 LET ET=0
 320 FOR E=1 TO 300
 330 PRINT AT 3,0;"EPOCA: ";E; " ERRORES ANTERIOR=";ET;"   "
 335 LET ET=0

 340 FOR I=1 TO N
 350 FOR J=1 TO K

 360 REM Z = B(J) + SUM X(I,P)*W(J,P)
 370 LET Z=B(J)
 380 FOR P=1 TO M
 390 LET Z=Z + X((I-1)*M+P)*W((J-1)*M+P)
 400 NEXT P

 410 REM SALIDA BINARIA (ESCALON)
 420 IF Z>0 THEN LET Y=1
 430 IF Z<=0 THEN LET Y=0

 440 REM OBJETIVO ONE-VS-REST
 450 LET T=0
 460 IF C(I)=J THEN LET T=1

 470 LET ER=T-Y
 480 IF ER<>0 THEN LET ET=ET+1

 490 REM ACTUALIZAR PESOS Y SESGO
 500 FOR P=1 TO M
 510 LET W((J-1)*M+P)=W((J-1)*M+P) + R*ER*X((I-1)*M+P)
 520 NEXT P
 530 LET B(J)=B(J) + R*ER

 540 NEXT J
 550 NEXT I
 560 IF ET=0 THEN GO TO 600
 570 NEXT E
 580 PRINT "AVISO: NO CONVERGE TOTALMENTE"

 600 PRINT "ENTRENADO EN ";E;" EPOCAS!":PRINT "  ERRORES ULTIMA=";ET

 610 REM --- TEST INTERACTIVO (ARGMAX SOBRE Z BRUTO) ---
 620 PRINT
 630 PRINT "INTRODUCE ATRIBUTOS (0-100)."
 640 PRINT "PON -1 PARA SALIR."

 650 PRINT "MAGIA (0-100)? "
 660 INPUT A
 670 IF A<0 THEN STOP

 680 PRINT "FUERZA (0-100)? "
 690 INPUT F
 700 IF F<0 THEN STOP

 710 PRINT "VELOCIDAD (0-100)? "
 720 INPUT V
 730 IF V<0 THEN STOP

 740 LET X1=A/100
 750 LET X2=F/100
 760 LET X3=V/100

 770 REM Z1,Z2,Z3 (SIN ESCALON)
 780 LET Z1=B(1) + X1*W(1) + X2*W(2) + X3*W(3)
 790 LET Z2=B(2) + X1*W(4) + X2*W(5) + X3*W(6)
 800 LET Z3=B(3) + X1*W(7) + X2*W(8) + X3*W(9)

 810 LET MX=Z1: LET CL=1
 820 IF Z2>MX THEN LET MX=Z2: LET CL=2
 830 IF Z3>MX THEN LET MX=Z3: LET CL=3

 840 INK 3
 850 IF CL=1 THEN PRINT "RESULTADO: MAGO"
 860 IF CL=2 THEN PRINT "RESULTADO: GUERRERO"
 870 IF CL=3 THEN PRINT "RESULTADO: EXPLORADOR"
 880 INK 0

 890 PRINT "Z(MAGO)=";Z1;"  Z(GUERR)=";Z2;"  Z(EXP)=";Z3
 900 PRINT
 910 GO TO 650


9000 REM ==========================
9010 REM === DATOS ENTRENAMIENTO ===
9020 REM FORMATO: MAGIA,FUERZA,VELOCIDAD,CLASE
9030 REM ==========================

9040 REM --- MAGOS ---
9050 DATA 90,20,40,1
9060 DATA 85,25,55,1
9070 DATA 95,15,35,1
9080 DATA 80,30,50,1
9090 DATA 88,18,60,1
9100 DATA 92,22,45,1
9110 DATA 78,28,65,1
9120 DATA 83,20,30,1
9130 DATA 87,35,40,1
9140 DATA 93,10,55,1

9150 REM --- GUERREROS ---
9160 DATA 15,90,40,2
9170 DATA 20,85,35,2
9180 DATA 10,95,30,2
9190 DATA 25,80,50,2
9200 DATA 18,88,55,2
9210 DATA 12,92,45,2
9220 DATA 30,78,40,2
9230 DATA 22,83,60,2
9240 DATA 17,97,25,2
9250 DATA 28,86,35,2

9260 REM --- EXPLORADORES ---
9270 DATA 25,45,90,3
9280 DATA 20,50,85,3
9290 DATA 30,40,95,3
9300 DATA 35,55,80,3
9310 DATA 18,60,88,3
9320 DATA 40,42,92,3
9330 DATA 22,48,78,3
9340 DATA 15,52,96,3
9350 DATA 32,38,89,3
9360 DATA 27,58,83,3