  10 REM =========================================
  20 REM 3 EN RAYA (TIC-TAC-TOE) - SINCLAIR BASIC
  30 REM MOTOR + MAQUINA ALEATORIA 
  40 REM B(1..9): 0=VACIO, 1=HUMANO, 2=MAQUINA
  50 REM J1$ y J2$: simbolos del humano y la maquina
  60 REM =========================================
  65 RUN AT 3
  70 DIM B(9)
  75 DIM NXT(9)
  76 DIM SJ(9)
  77 DIM SP(9)
  78 DIM BST(9)
  79 DIM ORD(9)
  80 DIM AL(9)
  81 DIM BE(9)

  90 RANDOMIZE
  95 LET WIN=0: LET DRAWS=0
 100 LET HUMAN=1: LET CPU=2
 105 REM ORDEN DE JUGADAS: centro, esquinas, lados
 106 LET ORD(1)=5
 107 LET ORD(2)=1: LET ORD(3)=3: LET ORD(4)=7: LET ORD(5)=9
 108 LET ORD(6)=2: LET ORD(7)=4: LET ORD(8)=6: LET ORD(9)=8
 110 CLS
 120 PRINT "3 EN RAYA (PODA)"
 130 PRINT
 140 PRINT "ELIGE TU SIMBOLO:"
 150 PRINT "1) X"
 160 PRINT "2) O"
 170 INPUT K
 180 IF K<>1 AND K<>2 THEN GO TO 110
 190 IF K=1 THEN LET J1$="X": LET J2$="O"
 200 IF K=2 THEN LET J1$="O": LET J2$="X"

 210 CLS
 220 PRINT "QUIEN EMPIEZA?"
 230 PRINT "1) TU"
 240 PRINT "2) MAQUINA"
 250 INPUT S
 260 IF S<>1 AND S<>2 THEN GO TO 210
 270 IF S=1 THEN LET TURN=HUMAN
 280 IF S=2 THEN LET TURN=CPU

 290 FOR I=1 TO 9: LET B(I)=0: NEXT I

 300 REM ---------- BUCLE PRINCIPAL ----------
 310 CLS
 320 GO SUB 1000
 330 GO SUB 2000
 340 IF WIN<>0 THEN GO TO 900
 350 GO SUB 2200
 360 IF DRAWS=1 THEN GO TO 900

 370 IF TURN=HUMAN THEN GO TO 500
 380 GO TO 700

 500 REM ---------- TURNO HUMANO ----------
 510 PRINT
 520 PRINT "TU TURNO (";J1$;"). CASILLA 1..9:";
 530 INPUT P
 540 IF P<1 OR P>9 THEN GO TO 600
 550 IF B(P)<>0 THEN GO TO 600
 560 LET B(P)=HUMAN
 570 LET TURN=CPU
 580 GO TO 300

 600 PRINT "MOVIMIENTO INVALIDO. ENTER."
 610 INPUT A$
 620 GO TO 300

 700 REM ---------- TURNO MAQUINA  ----------
 710 GO SUB 3000
 720 LET B(POS)=CPU
 730 LET TURN=HUMAN
 740 GO TO 300

 900 REM ---------- FIN ----------
 910 CLS
 920 GO SUB 1000
 930 PRINT
 940 IF WIN=HUMAN THEN PRINT "GANAS TU (";J1$;")."
 950 IF WIN=CPU THEN PRINT "GANA LA MAQUINA (";J2$;")."
 960 IF WIN=0 THEN PRINT "EMPATE."
 970 PRINT
 980 PRINT "OTRA PARTIDA? (S/N)";
 990 INPUT R$
 992 IF R$="S" OR R$="s" THEN GO TO 110
 999 STOP

1000 REM ---------- DIBUJAR TABLERO ----------
1010 PRINT "   1   2   3"
1020 PRINT " +---+---+---+"
1030 GO SUB 1200: PRINT "1| ";A$;" | ";B$;" | ";C$;" |"
1040 PRINT " +---+---+---+"
1050 GO SUB 1230: PRINT "2| ";A$;" | ";B$;" | ";C$;" |"
1060 PRINT " +---+---+---+"
1070 GO SUB 1260: PRINT "3| ";A$;" | ";B$;" | ";C$;" |"
1080 PRINT " +---+---+---+"
1090 RETURN

1200 REM fila 1: casillas 1,2,3 -> A$,B$,C$
1210 LET I=1: GO SUB 1300: LET A$=C$
1220 LET I=2: GO SUB 1300: LET B$=C$
1225 LET I=3: GO SUB 1300: LET C$=C$
1228 RETURN

1230 REM fila 2: casillas 4,5,6
1240 LET I=4: GO SUB 1300: LET A$=C$
1250 LET I=5: GO SUB 1300: LET B$=C$
1255 LET I=6: GO SUB 1300: LET C$=C$
1258 RETURN

1260 REM fila 3: casillas 7,8,9
1270 LET I=7: GO SUB 1300: LET A$=C$
1280 LET I=8: GO SUB 1300: LET B$=C$
1285 LET I=9: GO SUB 1300: LET C$=C$
1288 RETURN

1300 REM ---------- CELDA A TEXTO (C$) ----------
1310 LET V=B(I)
1320 LET C$=" "
1330 IF V=HUMAN THEN LET C$=J1$
1340 IF V=CPU THEN LET C$=J2$
1350 RETURN

2000 REM ---------- COMPROBAR GANADOR (WIN) ----------
2010 LET WIN=0

2020 REM filas
2030 FOR R=0 TO 2
2040 LET I=1+R*3
2050 IF B(I)<>0 AND B(I)=B(I+1) AND B(I)=B(I+2) THEN LET WIN=B(I)
2060 NEXT R
2070 IF WIN<>0 THEN RETURN

2080 REM columnas
2100 FOR C=1 TO 3
2110 IF B(C)<>0 AND B(C)=B(C+3) AND B(C)=B(C+6) THEN LET WIN=B(C)
2120 NEXT C
2130 IF WIN<>0 THEN RETURN

2135 REM diagonales
2140 IF B(1)<>0 AND B(1)=B(5) AND B(1)=B(9) THEN LET WIN=B(1)
2150 IF B(3)<>0 AND B(3)=B(5) AND B(3)=B(7) THEN LET WIN=B(3)
2160 RETURN

2200 REM ---------- COMPROBAR EMPATE (DRAWS) ----------
2210 LET DRAWS=1
2220 FOR I=1 TO 9
2230 IF B(I)=0 THEN LET DRAWS=0
2240 NEXT I
2250 RETURN

3000 REM =========================================
3010 REM BUSQUEDA POR MINIMAX COMPLETO (con poda)
3020 REM Devuelve POS con la mejor casilla para CPU
3030 REM =========================================

3050 LET POS=0
3052 REM --- ATAJO 1: si tablero vacio, centro ---
3054 LET EMPTY=1
3056 FOR T=1 TO 9
3058   IF B(T)<>0 THEN LET EMPTY=0
3060 NEXT T
3062 IF EMPTY=1 THEN LET POS=5: RETURN

3064 REM --- ATAJO 2: si CPU gana en 1, hazlo ---
3066 LET PLAYER=CPU
3068 GO SUB 3600
3070 IF POS<>0 THEN RETURN

3072 REM --- ATAJO 3: si HUMANO gana en 1, bloquea ---
3074 LET PLAYER=HUMAN
3076 GO SUB 3600
3078 IF POS<>0 THEN RETURN

3080 REM --- si no, ya sí minimax ---
3090 LET BESTS=-2
3100 LET NODES=0
3105 LET DEP=0
3107 PRINT AT 0,0;"NODOS: 0    ": REM ****************************************

3110 FOR K=1 TO 9
3112 LET M=ORD(K)
3114 IF B(M)<>0 THEN GO TO 3160
3120 LET B(M)=CPU
3125 LET PROX=HUMAN
3126 LET ALPHA=-2: LET BETA=2
3130 GO SUB 3200
3135 LET B(M)=0
3140 IF SCORE>BESTS THEN LET BESTS=SCORE: LET POS=M
3150 IF BESTS=1 THEN RETURN
3160 NEXT K

3170 IF POS=0 THEN GO SUB 3420: REM fallback por seguridad
3180 RETURN

3200 REM -----------------------------------------
3210 REM MINIMAX(PROX) con ALFA-BETA -> SCORE
3220 REM Entradas: PROX, ALPHA, BETA
3230 REM Salida : SCORE (+1 gana CPU, 0 empate, -1 pierde CPU)
3240 REM -----------------------------------------

3245 LET DEP=DEP+1
3250 LET SP(DEP)=PROX
3252 LET AL(DEP)=ALPHA
3254 LET BE(DEP)=BETA

3255 LET NODES=NODES+1
3256 IF NODES-INT(NODES/256)*256=0 THEN PRINT AT 0,0;"NODOS: ";NODES;"   "

3260 GO SUB 2000
3265 IF WIN=CPU THEN LET SCORE=1: GO TO 3490
3270 IF WIN=HUMAN THEN LET SCORE=-1: GO TO 3490

3275 GO SUB 2200
3280 IF DRAWS=1 THEN LET SCORE=0: GO TO 3490

3285 REM Inicializa BEST según jugador
3290 IF PROX=CPU THEN LET BST(DEP)=-2
3295 IF PROX=HUMAN THEN LET BST(DEP)=2
3300 LET NXT(DEP)=1

3305 REM ---- bucle manual KK=1..9 usando ORD() ----
3310 LET KK=NXT(DEP)
3315 IF KK>9 THEN LET SCORE=BST(DEP): GO TO 3490

3320 LET J=ORD(KK)
3322 IF B(J)<>0 THEN LET NXT(DEP)=KK+1: GO TO 3310

3325 LET SJ(DEP)=J
3330 LET B(J)=PROX

3335 REM Cambiar turno
3337 LET OLD=PROX
3340 IF OLD=CPU THEN LET PROX=HUMAN
3345 IF OLD=HUMAN THEN LET PROX=CPU

3348 LET NXT(DEP)=KK+1
3350 GO SUB 3200
3352 LET CHILD=SCORE

3355 REM Restaurar estado
3360 LET PROX=SP(DEP)
3365 LET B(SJ(DEP))=0

3370 REM Actualizar BEST y ALPHA/BETA
3375 IF PROX=CPU THEN IF CHILD>BST(DEP) THEN LET BST(DEP)=CHILD
3380 IF PROX=HUMAN THEN IF CHILD<BST(DEP) THEN LET BST(DEP)=CHILD

3385 IF PROX=CPU THEN IF BST(DEP)>ALPHA THEN LET ALPHA=BST(DEP)
3390 IF PROX=HUMAN THEN IF BST(DEP)<BETA THEN LET BETA=BST(DEP)

3395 REM Poda ALFA-BETA
3400 IF ALPHA>=BETA THEN LET SCORE=BST(DEP): GO TO 3490

3405 GO TO 3310

3490 REM ---- salida: restaurar y volver ----
3492 LET PROX=SP(DEP)
3494 LET ALPHA=AL(DEP)
3496 LET BETA=BE(DEP)
3498 LET DEP=DEP-1
3500 RETURN

3600 REM -----------------------------------------
3610 REM BUSCAR JUGADA GANADORA EN 1 (PLAYER) -> POS
3620 REM Devuelve POS=0 si no hay
3630 REM -----------------------------------------
3640 LET POS=0
3650 FOR T=1 TO 9
3660   IF B(T)<>0 THEN GO TO 3710
3670   LET B(T)=PLAYER
3680   GO SUB 2000
3690   LET B(T)=0
3695   IF WIN=PLAYER THEN LET POS=T: LET WIN=0: RETURN
3700   LET WIN=0
3710 NEXT T
3720 RETURN